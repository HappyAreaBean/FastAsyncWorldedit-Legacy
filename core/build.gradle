repositories {
    flatDir {dirs 'lib'}
}
dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'org.yaml:snakeyaml:1.16'
    implementation 'com.google.code.gson:gson:2.2.4'
    implementation 'net.fabiozumbi12:redprotect:1.9.6'
    implementation 'com.plotsquared:plotsquared-api:3.1'
    implementation "org.primesoft:BlocksHub:3.1.1"
    implementation 'com.github.luben:zstd-jni:1.1.1'
//    implementation 'org.javassist:javassist:3.22.0-CR1'
    implementation 'co.aikar:fastutil-lite:1.0'
    implementation(group: 'com.sk89q.worldedit', name: 'worldedit-core', version:'6.1.3-SNAPSHOT') {
        exclude(module: 'bukkit-classloader-check')
    }
}

processResources {
    from('src/main/resources') {
        include 'fawe.properties'
        expand(
                version: "${project.parent.version}",
                name: project.parent.name,
        )
    }
}

jar.archiveName="fawe-api-${project.parent.version}.jar"
jar.destinationDir = file '../mvn/com/boydti/fawe-api/' + project.parent.version
task createPom {
    doLast {
        pom {
            project {
                groupId 'com.boydti'
                artifactId 'fawe-api'
                version project.parent.version
            }
        }
                .getEffectivePom()
                .setDependencies(new ArrayList<>())
                .writeTo("../mvn/com/boydti/fawe-api/${project.parent.version}/fawe-api-${project.parent.version}.pom")
        pom {
            project {
                groupId 'com.boydti'
                artifactId 'fawe-api'
                version 'latest'
            }
        }
                .getEffectivePom()
                .setDependencies(new ArrayList<>())
                .writeTo("../mvn/com/boydti/fawe-api/latest/fawe-api-latest.pom")
    }
}

task copyFiles {
    doLast {
        copy {
            from "../mvn/com/boydti/fawe-api/${project.parent.version}/"
            into '../mvn/com/boydti/fawe-api/latest/'
            include('*.jar')
            rename ("fawe-api-${project.parent.version}.jar", 'fawe-api-latest.jar')
        }
    }
}

build.finalizedBy(copyFiles)
copyFiles.dependsOn(createPom)
