repositories {
    flatDir {dirs 'lib'}
}
dependencies {
    implementation project(':core')
    implementation 'com.sk89q:worldguard:6.0.0-SNAPSHOT'
    implementation('com.destroystokyo.paper:paper-api:1.12-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5'
    }
    implementation 'org.bukkit.craftbukkit:Craftbukkit_1_12:1.12.1'
    implementation 'org.bukkit.craftbukkit:Craftbukkit_1_11:1.11'
    implementation 'org.bukkit.craftbukkit:Craftbukkit_1_10:1.10'
    implementation 'org.bukkit.craftbukkit:Craftbukkit_1_9:1.9.4'
    implementation 'org.bukkit.craftbukkit:Craftbukkit_1_8:1.8.8'
    implementation 'org.bukkit.craftbukkit:Craftbukkit_1_7:1.7.10'
    implementation "com.github.MilkBowl:VaultAPI:1.7"
    implementation 'io.papermc:paperlib:1.0.6'
    implementation 'com.massivecraft.factions:Factions:3.3.0'
    implementation 'com.factionsone:FactionsOne:1.2.2'
    implementation 'me.ryanhamshire:GriefPrevention:11.5.2'
    implementation 'com.massivecraft:mcore:7.0.1'
    implementation 'net.jzx7:regios:5.9.9'
    implementation 'com.bekvon.bukkit.residence:Residence:4.5._13.1'
    implementation 'com.palmergames.bukkit:towny:0.84.0.9'
    implementation "org.primesoft:BlocksHub:3.1.1"
    implementation 'com.worldcretornica:plotme_core:0.16.3'
    implementation 'junit:junit:4.11'
    implementation 'com.sk89q.worldedit:worldedit-bukkit:6.1.4-SNAPSHOT'
    implementation 'com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT'
    implementation 'com.thevoxelbox.voxelsniper:voxelsniper:5.171.0'
    implementation 'com.comphenix.protocol:ProtocolLib-API:4.4.0'
    implementation 'com.wasteofplastic:askyblock:3.0.9.4'
    implementation('org.inventivetalent:mapmanager:1.7.2-SNAPSHOT') {
        transitive = false
    }
}

clean { delete "../target" }

processResources {
    from('src/main/resources') {
        include 'plugin.yml'
        expand(
                name: project.parent.name,
                version: project.parent.version
        )
    }
}

jar.archiveName="fawe-bukkit-${project.parent.version}.jar"
jar.destinationDir = file '../mvn/com/boydti/fawe-bukkit/' + project.parent.version
task createPom {
    doLast {
        pom {
            project {
                groupId 'com.boydti'
                artifactId 'fawe-bukkit'
                version project.parent.version
            }
        }
                .getEffectivePom()
                .setDependencies(new ArrayList<>())
                .writeTo("../mvn/com/boydti/fawe-bukkit/${project.parent.version}/fawe-bukkit-${project.parent.version}.pom")
        pom {
            project {
                groupId 'com.boydti'
                artifactId 'fawe-bukkit'
                version 'latest'
            }
        }
                .getEffectivePom()
                .setDependencies(new ArrayList<>())
                .writeTo("../mvn/com/boydti/fawe-bukkit/latest/fawe-bukkit-latest.pom")
    }
}
task copyFiles {
    doLast {
        copy {
            from "../mvn/com/boydti/fawe-bukkit/${project.parent.version}/"
            into '../mvn/com/boydti/fawe-bukkit/latest/'
            include('*.jar')
            rename ("fawe-bukkit-${project.parent.version}.jar", 'fawe-bukkit-latest.jar')
        }
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
// We only want the shadow jar produced
shadowJar {
    dependencies {
        include(dependency('com.github.luben:zstd-jni:1.1.1'))
        include(dependency('co.aikar:fastutil-lite:1.0'))
        include(dependency(':core'))
        include(dependency('io.papermc:paperlib:1.0.6'))
    }
    archiveName = "${parent.name}-${project.name}-${parent.version}.jar"
    destinationDir = file '../target'
    relocate('com.google.gson', 'com.sk89q.worldedit.internal.gson')
}
shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}

build.dependsOn(shadowJar);
