repositories {
    flatDir {dirs 'lib'}
}
dependencies {
    compile project(':core')
    compile 'com.sk89q:worldguard:6.0.0-SNAPSHOT'
    compile('com.destroystokyo.paper:paper-api:1.12-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5'
    }
    compile 'org.bukkit.craftbukkit:Craftbukkit_1_12:1.12.1'
    compile 'org.bukkit.craftbukkit:Craftbukkit_1_11:1.11'
    compile 'org.bukkit.craftbukkit:Craftbukkit_1_10:1.10'
    compile 'org.bukkit.craftbukkit:Craftbukkit_1_9:1.9.4'
    compile 'org.bukkit.craftbukkit:Craftbukkit_1_8:1.8.8'
    compile 'org.bukkit.craftbukkit:Craftbukkit_1_7:1.7.10'
    compile "com.github.MilkBowl:VaultAPI:1.7"
    compile 'io.papermc:paperlib:1.0.6'
    compile 'com.massivecraft:factions:2.8.0'
    compile 'com.factionsone:FactionsOne:1.2.2'
    compile 'me.ryanhamshire:GriefPrevention:11.5.2'
    compile 'com.massivecraft:mcore:7.0.1'
    compile 'net.jzx7:regios:5.9.9'
    compile 'com.bekvon.bukkit.residence:Residence:4.5._13.1'
    compile 'com.palmergames.bukkit:towny:0.84.0.9'
    compile 'com.worldcretornica:plotme_core:0.16.3'
    compile 'junit:junit:4.11'
    compile 'com.sk89q.worldedit:worldedit-bukkit:6.1.4-SNAPSHOT'
    compile 'com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT'
    compile 'com.thevoxelbox.voxelsniper:voxelsniper:5.171.0'
    compile 'com.comphenix.protocol:ProtocolLib-API:4.4.0'
    compile('org.inventivetalent:mapmanager:1.7.2-SNAPSHOT') {
        transitive = false
    }
}

clean { delete "../target" }

processResources {
    from('src/main/resources') {
        include 'plugin.yml'
        expand(
                name: project.parent.name,
                version: project.parent.version
        )
    }
}

jar.archiveName="fawe-bukkit-${project.parent.version}.jar"
jar.destinationDir = file '../mvn/com/boydti/fawe-bukkit/' + project.parent.version
task createPom << {
    pom {
        project {
            groupId 'com.boydti'
            artifactId 'fawe-bukkit'
            version project.parent.version
        }
    }
    .getEffectivePom()
            .setDependencies(new ArrayList<>())
            .writeTo("../mvn/com/boydti/fawe-bukkit/${project.parent.version}/fawe-bukkit-${project.parent.version}.pom")
    pom {
        project {
            groupId 'com.boydti'
            artifactId 'fawe-bukkit'
            version 'latest'
        }
    }
    .getEffectivePom()
            .setDependencies(new ArrayList<>())
            .writeTo("../mvn/com/boydti/fawe-bukkit/latest/fawe-bukkit-latest.pom")
}
task copyFiles {
    doLast {
        copy {
            from "../mvn/com/boydti/fawe-bukkit/${project.parent.version}/"
            into '../mvn/com/boydti/fawe-bukkit/latest/'
            include('*.jar')
            rename ("fawe-bukkit-${project.parent.version}.jar", 'fawe-bukkit-latest.jar')
        }
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
// We only want the shadow jar produced
shadowJar {
    dependencies {
        include(dependency('com.github.luben:zstd-jni:1.1.1'))
        include(dependency('co.aikar:fastutil-lite:1.0'))
        include(dependency(':core'))
        include(dependency('io.papermc:paperlib:1.0.6'))
    }
    archiveName = "${parent.name}-${project.name}-${parent.version}.jar"
    destinationDir = file '../target'
    relocate('com.google.gson', 'com.sk89q.worldedit.internal.gson')
}
shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}

build.dependsOn(shadowJar);
